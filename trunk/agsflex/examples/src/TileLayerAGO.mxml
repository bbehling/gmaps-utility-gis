<?xml version="1.0" encoding="utf-8"?>
<mx:Application xmlns:mx="http://www.adobe.com/2006/mxml"
                layout="absolute">
   <mx:UIComponent id="mapHolder"
                    creationComplete="onHolderCreated(event)"
                    resize="onHolderResized(event)"
                    width="100%"
                    height="100%"/>
  <mx:Script>
    <![CDATA[
      import com.google.maps.controls.*;
      import com.google.maps.*;
      import ags.*; 
   
       private var map:Map;
      private var svc:MapService;
      SpatialReferences.addSpatialReference(2264, 'PROJCS["NAD_1983_StatePlane_North_Carolina_FIPS_3200_Feet",GEOGCS["GCS_North_American_1983",DATUM["D_North_American_1983",SPHEROID["GRS_1980",6378137.0,298.257222101]],PRIMEM["Greenwich",0.0],UNIT["Degree",0.0174532925199433]],PROJECTION["Lambert_Conformal_Conic"],PARAMETER["False_Easting",2000000.002616666],PARAMETER["False_Northing",0.0],PARAMETER["Central_Meridian",-79.0],PARAMETER["Standard_Parallel_1",34.33333333333334],PARAMETER["Standard_Parallel_2",36.16666666666666],PARAMETER["Latitude_Of_Origin",33.75],UNIT["Foot_US",0.3048006096012192]]');

      public function onHolderCreated(event:Event):void {
        svc=new MapService('http://sampleserver1.arcgisonline.com/ArcGIS/rest/services/Portland/Portland_ESRI_LandBase_AGO/MapServer');
        svc.addEventListener(ServiceEvent.LOAD, onServiceLoad);
      }

      public function onHolderResized(event:Event):void {
        if (map != null) {
          map.setSize(new Point(mapHolder.width, mapHolder.height));
        }
      }

      public function onServiceLoad(event:ServiceEvent):void {
        // map should be constructed after service load instead from MXML, if you do not want load Google Tiles at all. 
        map=new Map();
        map.key="ABQIAAAA7QUChpcnvnmXxsjC7s1fCxQGj0PqsCtxKvarsoS-iqLdqZSKfxTd7Xf-2rEc_PC9o8IsJde80Wnj4g";
        map.width=mapHolder.width;
        map.height=mapHolder.height;
        mapHolder.addChild(map);
        map.addEventListener(MapEvent.MAP_PREINITIALIZE, onMapPreinitialize);
        map.addEventListener(MapEvent.MAP_READY, onMapReady);
      }

      private function onMapPreinitialize(event:Event):void {
        var agsLayer:ArcGISTileLayer=new ArcGISTileLayer(svc);
        var agsMapType:ArcGISMapType=new ArcGISMapType([agsLayer], new ArcGISMapTypeOptions({name: agsLayer.getName().replace(/ /g, '\n')}));
        var bnds:LatLngBounds=agsLayer.getInitialBounds();
        var opts:MapOptions=new MapOptions();
        opts.zoom=15;
        opts.center=new LatLng(45.5, -122.7);
        opts.mapTypes=[agsMapType].concat(MapType.DEFAULT_MAP_TYPES);
        opts.mapType = agsMapType;
        map.setInitOptions(opts);
      }
      
      
      private function onMapReady(event:Event):void {
        map.enableScrollWheelZoom();
        map.addControl(new MapTypeControl());
        map.addControl(new NavigationControl());
      }
    ]]>
  </mx:Script>
</mx:Application>
