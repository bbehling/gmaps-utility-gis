<?xml version="1.0" encoding="utf-8"?>
<mx:Application xmlns:mx="http://www.adobe.com/2006/mxml"
                layout="absolute"
                width="100%"
                height="100%">
  <mx:Panel title="Reverse Geocode: Click on Map or Drag Marker"
            width="100%"
            height="100%">
    <maps:Map xmlns:maps="com.google.maps.*"
              id="map"
              mapevent_mapready="onMapReady(event)"
              width="100%"
              height="100%"
              key="ABQIAAAA7QUChpcnvnmXxsjC7s1fCxQGj0PqsCtxKvarsoS-iqLdqZSKfxTd7Xf-2rEc_PC9o8IsJde80Wnj4g"/>
  </mx:Panel>
  <mx:Script>
    <![CDATA[
      import mx.events.ListEvent;
      import mx.controls.*;
      import mx.controls.dataGridClasses.*;
      import com.google.maps.controls.*;
      import com.google.maps.overlays.*;
      import com.google.maps.*;
      import com.google.maps.interfaces.*;

      import ags.gmaps.*; 
      import ags.service.*;
      import ags.sr.*;
      
      private var svc: GeocodeService;
      private var marker:Marker;


      private function onMapReady(event:Event):void {
        map.setCenter(new LatLng(34.0575170970001, -117.195681386), 15);
        map.addControl(new MapTypeControl());
        map.addControl(new NavigationControl());
        map.enableScrollWheelZoom();
        map.enableContinuousZoom();
        marker=new Marker(map.getCenter(), new MarkerOptions({draggable: true}));
        map.addOverlay(marker);
        svc=new GeocodeService('http://sampleserver1.arcgisonline.com/ArcGIS/rest/svcs/Locators/ESRI_Geocode_USA/GeocodeServer');

        svc.addEventListener(ServiceEvent.LOAD, function(evt:Event):void {
            map.addEventListener(MapMouseEvent.CLICK, function(evt:MapMouseEvent):void {
                reverseGeocode(evt.latLng);
              });
            marker.addEventListener(MapMouseEvent.DRAG_END, function(evt:MapMouseEvent):void {
                reverseGeocode(evt.latLng);
              });
            reverseGeocode(marker.getLatLng());
          });

      }

      private function reverseGeocode(latlng:LatLng):void {
        if (latlng) {
          var point:*=ArcGISUtil.fromLatLngToPoint(latlng, SpatialReferences.WGS84);
          var params:*={location: point, distance: 100};
          svc.reverseGeocode(params, function(result:*):void {
              if (result.address) {
                var html:String='';
                var attrs:*=result.address;
                for (var x:String in attrs) {
                  if (attrs.hasOwnProperty(x)) {
                    html+=x + ': ' + attrs[x] + '<br/>';
                  }
                }
                var loc:LatLng=ArcGISUtil.fromPointToLatLng(result.location, SpatialReferences.WGS84);
                marker.setLatLng(loc);
                marker.openInfoWindow(new InfoWindowOptions({contentHTML: html}));
              } else {
                Alert.show('can not find address for point:' + latlng.toString());
              }
            });
        }

      }
    ]]>
  </mx:Script>
</mx:Application>
